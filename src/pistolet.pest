Program = { SOI ~ Prog ~ EOI }

Prog = {
    (FUN | LET | WHILE | IF | RETURN)+
}

TERM = {
    "{" ~
    (FUN | LET | WHILE | IF | RETURN)+
    ~ "}"
}

FUN_CALL = {
    FUN_NAME ~ "(" ~ EXPR_A ~ ("," ~ EXPR_A)* ~ ")"
}

EXPR_A = _{
    BOOL_EXPR | EXPR | NONE
}

NONE = {
    ""
}

BOOL_EXPR = { 
    BOOL_EXPR_T ~ ((and | or) ~ BOOL_EXPR_T)* 
}

BOOL_EXPR_T = _{ 
    (not)? ~ ( ("(" ~ BOOL_EXPR ~ ")") | FUN_CALL | BOOL | VAR_NAME | EQ_EXPR)
}

EQ_EXPR = {
    EXPR ~ eq ~ EXPR
}

EXPR = { 
    EXPR_T ~ ((add | sub | mul | div) ~ EXPR_T)* 
}

EXPR_T = _{ 
    ("(" ~ EXPR ~ ")") | FUN_CALL | FLOAT | INTEGER | VAR_NAME
}

PARA_LIST = {
 ("(" ~ VAR_NAME ~ ":" ~ TYPE_NAME ~ ")")+
}

RETURN = {
    "return" ~ EXPR_A ~ "."
}

FUN = {
    "fun" ~ FUN_NAME ~ PARA_LIST ~ "->" ~ TYPE_NAME ~ TERM
}

LET = {
    "let" ~ VAR_NAME ~ ":" ~ TYPE_NAME ~ "=" ~ EXPR_A ~ "."
}

WHILE = {
    "while" ~ BOOL_EXPR ~ TERM
}

IF = {
   "if" ~ BOOL_EXPR ~ TERM ~ TERM
}

BOOL = { "true" | "false" }

alpha = { 'a'..'z' | 'A'..'Z' }

digit = { '0'..'9' }

int = @{ digit+ }

add = { "+" }

sub = { "-" }

mul = { "*" }   

div = { "/" }

and = { "&&" }

or = { "||" }   

not = { "~" }

eq = { "=?" }

FUN_NAME = @{ (alpha | "_") ~ (alpha | digit | "_")* }

TYPE_NAME = @{ (alpha | "_") ~ (alpha | digit | "_")* }

VAR_NAME = @{ (alpha | "_") ~ (alpha | digit | "_")* }

FLOAT = @{ ("-")? ~ int ~ ("." ~ int+) }

INTEGER = @{ ("-")? ~ int }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

COMMENT = _{ "(*" ~ (!"*)" ~ ANY)* ~ "*)" }